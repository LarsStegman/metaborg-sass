module Expression

imports
  Common

context-free start-symbols
  Exp
  
context-free syntax

  // Literals
  Exp           = Number
  Exp.String    = String
  Exp.HexColour = HEXCOLOUR
  Exp.Length    = <<Number> <LengthUnit>>
  Exp.True      = <true>
  Exp.False     = <false>
  Exp.Null      = <null>
  Exp.Variable  = <$<ID>>

  Exp = <(<Exp>)> {bracket}

  // Arithmetic Operators
  Exp.Add   = <<Exp> + <Exp>> {left}
  Exp.Sub   = <<Exp> - <Exp>> {left}
  Exp.Mult  = <<Exp> * <Exp>> {left}
  Exp.Div   = <<Exp> / <Exp>> {left}
  Exp.Mod   = <<Exp> % <Exp>> {left}
  
  // Logical Operators
  Exp.And = <<Exp> and <Exp>> {left}
  Exp.Or  = <<Exp> or <Exp>> {left}
  Exp.Not = <not <Exp>> {right}
  
  // Comparison
  Exp.Eq    = <<Exp> == <Exp>> {non-assoc}
  Exp.InEq  = <<Exp> != <Exp>> {non-assoc}
  Exp.Gt    = [[Exp] > [Exp]]  {non-assoc}
  Exp.Gte   = [[Exp] >= [Exp]] {non-assoc}
  Exp.Lt    = [[Exp] < [Exp]]  {non-assoc}
  Exp.Lte   = [[Exp] <= [Exp]] {non-assoc}
  
  // String
  String.Quoted = STRING
  
  Number.Int    = INT
  Number.Float  = FLOAT

context-free priorities
  {left:
      Exp.Not
  } >
  {left:
    Exp.Mult
    Exp.Div
    Exp.Mod
  } >
  {left:
      Exp.Add
      Exp.Sub
  } > {non-assoc:
      Exp.Gt
      Exp.Gte
      Exp.Lt
      Exp.Lte
  } >
  {left:
      Exp.And
  } > 
  {left:
    Exp.Or
  } >
  {non-assoc: 
      Exp.Eq
      Exp.InEq
  } 
  
  