module Expression

imports
  Common

context-free start-symbols
  Exp
  
context-free syntax

  // Literals
  Exp           = Number
  Exp.String    = String
  Exp.HexColour = HEXCOLOUR
  Exp.Length    = <<Number> <LengthUnit>>
  Exp.True      = <true>
  Exp.False     = <false>
  Exp.Null      = <null>
  Exp.Variable  = <$<ID>>

  Exp = <(<Exp>)> {bracket}

  // Arithmetic Operators
  Exp.Add   = <<Exp> + <Exp>>
  Exp.Sub   = <<Exp> - <Exp>>
  Exp.Mult  = <<Exp> * <Exp>>
  Exp.Div   = <<Exp> / <Exp>>
  Exp.Mod   = <<Exp> % <Exp>>
  
  // Logical Operators
  Exp.And = <<Exp> and <Exp>>
  Exp.Or  = <<Exp> or <Exp>>
  Exp.Not = <not <Exp>>
  
  // Comparison
  Exp.Eq    = <<Exp> == <Exp>>
  Exp.Ineq  = <<Exp> != <Exp>>
  Exp.Gt    = [[Exp] > [Exp]]
  Exp.Gte   = [[Exp] >= [Exp]]
  Exp.Lt    = [[Exp] < [Exp]]
  Exp.Lte   = [[Exp] <= [Exp]]
  
  // String
  String.Quoted     = STRING
  
  Number.Int    = INT
  Number.Float  = FLOAT

context-free priorities
  {left:
      Exp.Not
  } >
  {left:
    Exp.Mult
    Exp.Div
    Exp.Mod
  } >
  {left:
      Exp.Add
      Exp.Sub
  } > {left:
      Exp.Gt
      Exp.Gte
      Exp.Lt
      Exp.Lte
  } >
  {left:
      Exp.And
      Exp.Or
  } > 
  {left: 
      Exp.Eq
      Exp.Ineq
  } 
  
  