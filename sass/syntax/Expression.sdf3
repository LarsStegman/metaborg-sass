module Expression

imports
  Common
  Colour
  String
  Interpolate

context-free start-symbols
  Exp

/**
 * Imports
 */
context-free syntax
  Exp = Number
  Exp = Colour
  Exp = Interpolate
  Exp = String {avoid}

/**
 * Sorts
 */
context-free syntax
  Exp.Length    = <<Exp> <LengthUnit>> {prefer}
  Exp.True      = <true>
  Exp.False     = <false>
  Exp.Null      = <null>
  
  Exp.VarRef    = <<VAR>>

  Exp = <(<Exp>)> {bracket}

/**
 * Arithmetic Operators
 */
context-free syntax  
  Exp.Add   = <<Exp> + <Exp>> {left}
  Exp.Sub   = <<Exp> - <Exp>> {left}
  Exp.Mult  = <<Exp> * <Exp>> {left}
  Exp.Div   = <<Exp> / <Exp>> {left}
  Exp.Mod   = <<Exp> % <Exp>> {left}

/**
 * Logical Operators
 */  
context-free syntax
  Exp.And = <<Exp> and <Exp>> {left}
  Exp.Or  = <<Exp> or <Exp>> {left}
  Exp.Not = <not <Exp>> {right}
  
/**
 * Comparison Operators
 */
context-free syntax
  Exp.Eq    = <<Exp> == <Exp>> {non-assoc}
  Exp.InEq  = <<Exp> != <Exp>> {non-assoc}
  Exp.Gt    = [[Exp] > [Exp]]  {non-assoc}
  Exp.Gte   = [[Exp] >= [Exp]] {non-assoc}
  Exp.Lt    = [[Exp] < [Exp]]  {non-assoc}
  Exp.Lte   = [[Exp] <= [Exp]] {non-assoc}

/**
 * Numbers
 */
context-free syntax  
  Number.Int    = INT
  Number.Float  = FLOAT 

context-free priorities
  {non-assoc:
    Exp.Length
  } >
  {left:
      Exp.Not
  } >
  {left:
    Exp.Mult
    Exp.Div
    Exp.Mod
  } >
  {left:
      Exp.Add
      Exp.Sub
  } > {non-assoc:
      Exp.Gt
      Exp.Gte
      Exp.Lt
      Exp.Lte
  } >
  {left:
      Exp.And
  } > 
  {left:
    Exp.Or
  } >
  {non-assoc: 
      Exp.Eq
      Exp.InEq
  }


