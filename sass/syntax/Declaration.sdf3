module Declaration

imports 
  Common
  Expression
  Selector

context-free start-symbols
  ScriptItem

template options
  tokenize: ";()[]"

/*
 * General declarations
 */
context-free syntax

  Declaration = ScriptItem
  ScriptItem.BlockDeclaration = <
  <Selector> {
    <Declaration*>
  }
  
  >
  ScriptItem.VarDeclaration = <<VAR> : <PropertyExp> <VariableDeclarationFlag*>;>
  
  VariableDeclarationFlag.Global  = <!global>
  VariableDeclarationFlag.Default = <!default>
  
  ScriptItem.MixinDeclarationNoArgs = <
  @mixin <ID> {
    <Declaration*>
  }
  
  >
  ScriptItem.MixinDeclaration = <
  @mixin <ID>(<{FArgument ","}*>) {
    <Declaration*>
  }
  
  >
  
  FArgument.FArg = <<VAR>>
  FArgument.FArgWithDefault = <<VAR>: <PropertyExp>>


/*
 * Namespaced property declarations
 */
context-free syntax

  /* 
   * TODO: Property declarations need not be followed by a ; per se, 
   * however this is not true when there are multiple consecutive
   * property declarations. 
   */
  Declaration = PropertyDeclaration
  PropertyDeclaration.PropertyDeclaration = <<PROPERTY> : <PropertyExp>;> 
  PropertyDeclaration.NamespacePropertyDeclaration = <
  <PROPERTY>: {
    <PropertyDeclaration*>
  }
  
  >

syntax
  // Using kernel syntax, since layout matters to differentiate between 
  // namespaces and pseudoclasses
  PropertyDeclaration-CF.NamespacePropertyDeclarationExp = 
  PROPERTY-LEX ":" LAYOUT-CF PropertyExp-CF LAYOUT-CF "{"
    LAYOUT?-CF PropertyDeclaration*-CF LAYOUT?-CF
  "}"

context-free priorities
  PropertyDeclaration.NamespacePropertyDeclaration > ScriptItem.BlockDeclaration
