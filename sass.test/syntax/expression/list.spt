module test-syntax-list

language sass

fixture [[
.main {
  e: [[...]];
}
]]

test space separated list [[
1 2 3 4
]] parse to SpaceSeparatedList(Int("1"), [Int("2"), Int("3"), Int("4")])

test comma separated list [[
1, 3
]] parse to CommaSeparatedList(Int("1"), [Int("3")])

test space separated in comma separated [[
1, 2 3
]] parse to CommaSeparatedList(Int("1"), [SpaceSeparatedList(Int("2"), [Int("3")])])

test comma separated in space separated [[
1 (2, 3)
]] parse to SpaceSeparatedList(Int("1"), [CommaSeparatedList(Int("2"), [Int("3")])])

test nested list using brackets [[
(1 2) (3 4)
]] parse to SpaceSeparatedList(
              SpaceSeparatedList(Int("1"), [Int("2")]), 
              [SpaceSeparatedList(Int("3"), [Int("4")])]
            )

test empty list [[
()
]] parse to EmptyList()

test some list [[
4 5, green
]] parse to CommaSeparatedList(SpaceSeparatedList(Int("4"), [Int("5")]), [ColourLabel("green")])

test trailing commas [[
1, 4,
]] parse to CommaSeparatedList(Int("1"), [Int("4")])

test multiple trailing commas not allowed [[
1, 4,,
]] parse fails

test space separated list of empty lists [[
() () ()
]] parse to SpaceSeparatedList(EmptyList(), [EmptyList(), EmptyList()])

test comma separated list of empty lists [[
(), (), ()
]] parse to CommaSeparatedList(EmptyList(), [EmptyList(), EmptyList()])
